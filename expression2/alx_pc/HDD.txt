@name HDD
@inputs Bank1:wirelink Bank2:wirelink Bank3:wirelink Bank4:wirelink
@outputs HDD:wirelink
@persist HDD_BANK_SIZE HDD_SECTOR_SIZE HDD_SIZE HDD_BANK_MAX HDD_TYPE HDD_NAME:string
@persist HDD_Banks:array HDD_IO:entity
@trigger 
@model models/expression 2/cpu_controller.mdl

#[
    ALX Wiremod E2 PC
    Created by AlexALX
    -------------------
    HDD Bios - Handle HDD Banks 
    For Wire Dupeable HDD (DHDD)
]#

if (first()|duped()) {
    
HDD = wirelink()
HDD_IO = noentity()

HDD_BANK_SIZE = 256*1024
HDD_SECTOR_SIZE = 4
HDD_BANK_MAX = 4

HDD_TYPE = 0
HDD_NAME = "Wire DHDD" # vendor name

function sendData() {
    local Check = array()
    HDD_Banks = array()
    for (I=1,HDD_BANK_MAX) {
        local WEnt = ioGetInputWirelink("Bank"+I)
        local Ent = WEnt:entity()
        if (Ent:type()!="gmod_wire_dhdd" | Check[Ent:id(),number]) { continue }
        Check[Ent:id(),number] = 1
        HDD_Banks:pushWirelink(WEnt)
    }

    HDD_SIZE = HDD_BANK_SIZE*HDD_Banks:count()

    dsSendDirect("hddinit",HDD_IO,table(wirelink(),HDD_TYPE,HDD_NAME,HDD_SIZE,HDD_BANK_SIZE,HDD_SECTOR_SIZE,HDD_Banks))   
}

}

if (dsClk("init") & (HDD_IO==noentity() | dsGetTable()[1,entity]==HDD_IO)) {
    local Info = dsGetTable()
    HDD_IO = Info[1,entity]
    timer("sendData",Info[2,number])
}

if (dsClk("unlink") & HDD_IO==dsGetEntity()) {
    HDD_IO = noentity()
}

if (clk("sendData")) {
    sendData()   
}
