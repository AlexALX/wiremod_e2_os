@name Main GPU func
@inputs BIOS:entity EGP:wirelink DS:wirelink
@outputs E2_GPU:entity
@persist E2_CPU:entity E2_SPU:entity BIOS:entity HDD_IO:wirelink HDDS:table HDD_MAX 
@persist BIOS_INIT KeyHandler:string DsHandler:string ChatHandler:string ChatPlayer:entity GpuData:table BOOT_HDD
@persist LAST_I LAST_Y BIOS_X BIOS_SZ ERR_N ERR_C CMD:string CMDA:table Keyboard:wirelink ShutHandler:string
@persist FS_TYPES:table DEBUG INCLUDE

#[
	ALX Wiremod E2 PC
	Created by AlexALX
	-------------------
	Main gpu functions/variables + wire inputs/outputs
]#

if (first()|duped()) {
    
#include "alx_pc/shared/_main_"
    
INCLUDE = 0
    
function number in_array(Arr:array, Value) {
    for (I=1,Arr:count()) {
        if (Arr[I,number]==Value) { return 1 }
    }       
    return 0
}

function err() {
    dsSendDirect("err",BIOS,ERR_C)
    if (ERR_C!=-1) { exit() }
}

function runCmd(Cmd:string,Delay) {
    if (DEBUG) { Delay = 1 }
    CMD = Cmd
    timer("cmd",Delay)
}

function runCmd(Cmd:string,Delay,Arg:table) {
    if (DEBUG) { Delay = 1 }
    CMD = Cmd
    CMDA = Arg
    timer("cmd",Delay)
}

function egpClear() {
    EGP:egpClear()
    EGP:egpDrawTopLeft(1)
    LAST_I = 1 
    LAST_Y = 0 
    GpuData = table()
}

function dsClear() {
    DS[1048574] = 0
    DS[1048575] = 1
}

function reboot() {
    dsSendDirect("REBOOT",BIOS,0)   
}

function hddload(Num) {
    dsSendDirect("HDDInd",BIOS,Num)    
}

}
